# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//ui/ozone/ozone.gni")

if (use_efl) {
  import("//tizen_src/chromium_impl/ui/ui_efl.gni")
}

assert(use_x11 || ozone_platform_x11)

component("x") {
  output_name = "gfx_x11"

  sources = [
    "../gfx_export.h",
    "x11_atom_cache.cc",
    "x11_atom_cache.h",
    "x11_connection.cc",
    "x11_connection.h",
    "x11_error_tracker.cc",
    "x11_error_tracker.h",
    "x11_switches.cc",
    "x11_switches.h",
    "x11_types.cc",
    "x11_types.h",
  ]

  defines = [ "GFX_IMPLEMENTATION" ]

  configs += [ "//build/config/linux:x11" ]

  deps = [
    "//base",
  ]

  if (use_efl) {
    configs += external_ui_gfx_x11_configs
    sources += external_ui_gfx_x11_sources
    sources -= external_exclude_ui_gfx_x11_sources
  }
}
